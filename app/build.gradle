apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    applicationId "vn.tale.ub"
    minSdkVersion 15
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  productFlavors {
    pro {
      applicationId = "vn.tale.ub"
    }
    dev {
      applicationId = "vn.tale.ub.dev"
    }
    mock {
      applicationId = "vn.tale.ub.mock"
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lintOptions {
    warningsAsErrors true
    abortOnError true // Fail early.

    disable 'GoogleAppIndexingWarning' // App does not have deep linking.
    disable 'InvalidPackage' // Okio references java.nio that does not presented in Android SDK.
  }

  packagingOptions {
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }

  // This allows us see tests execution progress in the output on the CI.
  testOptions.unitTests.all {
    testLogging {
      events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
      exceptionFormat 'full'
    }
  }

}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'

  compile 'com.android.support:appcompat-v7:23.2.1'
  compile 'com.android.support:recyclerview-v7:23.2.1'

  compile 'io.reactivex:rxandroid:1.1.0'
  // Because RxAndroid releases are few and far between, it is recommended you also
  // explicitly depend on RxJava's latest version for bug fixes and new features.
  compile 'io.reactivex:rxjava:1.1.0'
  compile 'com.github.talenguyen:LceeBinding:2.0.0'
  compile 'com.github.bumptech.glide:glide:3.7.0'

  apt "org.immutables:value:2.1.15" // <-- for annotation processor
  provided "org.immutables:value:2.1.15" // <-- for annotation API
  provided "org.immutables:builder:2.1.15" // for annotations
  provided "org.immutables:gson:2.1.15" // for annotations

  compile 'com.google.dagger:dagger:2.1'
  apt 'com.google.dagger:dagger-compiler:2.1'
}
